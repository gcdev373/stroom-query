/*
 * Copyright 2017 Crown Copyright
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.moduleName = 'stroom.query.testing'

sourceSets {
    // This lets us use different directories for our integration tests.
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    // This means our integration tests get all the dependencies from our tests and we don't need to specify them twice.
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

// This task lets us run the actual integration tests.
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "standardOut", "started", "passed", "skipped", "failed"
    }
}

// These lines pull the integration test task into our build process.
check.dependsOn integrationTest
integrationTest.mustRunAfter test

// This means the reports from our integration tests won't over-write the reports from our unit tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    //--------Project----------
    implementation project(':stroom-docref')
    implementation project(':stroom-query-api')
    implementation project(':stroom-query-authorisation')
    implementation project(':stroom-query-audit')

    //--------3rd party--------
    implementation libs.dropwizard_auth
    implementation libs.dropwizard_core
    implementation libs.dropwizard_jooq
    implementation libs.dropwizard_testing
    implementation libs.guice
    implementation libs.hibernate_core
    implementation libs.hibernate_jpa
    implementation libs.javax_ws;
    implementation libs.jackson_core
    implementation libs.jackson_annotations
    implementation libs.jackson_databind
    implementation libs.jaxb_api
    implementation libs.jetty_http
    implementation libs.jooq
    implementation libs.junit_jupiter_api
    implementation libs.junit4
    implementation libs.jose4j
    implementation libs.slf4j_api
    implementation libs.wiremock
    implementation libs.mockito_core

    implementation libs.dropwizard_testing

    integrationTestImplementation libs.jackson_annotations
    integrationTestImplementation libs.dropwizard_flyway
    integrationTestImplementation project(':stroom-query-hibernate')
    integrationTestImplementation project(':stroom-query-jooq')
    integrationTestRuntimeOnly libs.mariadb_connector
    integrationTestRuntimeOnly 'org.apache.derby:derby:10.8.2.2'
}
